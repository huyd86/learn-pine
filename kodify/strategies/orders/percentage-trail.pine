// Source: https://kodify.net/tradingview/orders/percentage-trail/
// How to make a percentage-based trailing stop in TradingView Pine?

//=== Code a percentage-based trailing stop in TradingView

//=== Overview: steps to code percentage-based trailing stops

// Optional: make an input that configures the trailing percentage.
// Use that percentage to calculate the trailing stop price.
// Then submit the stop-loss order for the calculated price.

//====== Step 1. Optional: set trailing stop percentage with inputs

// Configure trail stop level with input options (optional)
longTrailPerc = input(title="Trail Long Loss (%)",
     type=float, minval=0.0, step=0.1, defval=3) * 0.01

shortTrailPerc = input(title="Trail Short Loss (%)",
     type=float, minval=0.0, step=0.1, defval=3) * 0.01

//====== Step 2. Determine the trail stop prices for longs and shorts

// Determine trail stop loss prices
longStopPrice = 0.0

longStopPrice := if (strategy.position_size > 0)
    stopValue = close * (1 - longTrailPerc)
    max(stopValue, longStopPrice[1])
else
    0

// Determine trailing short price
shortStopPrice = 0.0

shortStopPrice := if (strategy.position_size < 0)
    stopValue = close * (1 + shortTrailPerc)
    min(stopValue, shortStopPrice[1])
else
    999999

//====== Step 3. Submit the strategy's trailing stop orders

// Submit exit orders for trail stop loss price
if (strategy.position_size > 0)
    strategy.exit(id="XL TRL STP", stop=longStopPrice)

if (strategy.position_size < 0)
    strategy.exit(id="XS TRL STP", stop=shortStopPrice)

//=== Example strategy: trade with percentage-based trailing stops

//@version=3
strategy(title="Trailing stop loss (% of instrument price)",
     overlay=true, pyramiding=3)

// STEP 1:
// Configure trail stop level with input options (optional)
longTrailPerc = input(title="Trail Long Loss (%)",
     type=float, minval=0.0, step=0.1, defval=3) * 0.01

shortTrailPerc = input(title="Trail Short Loss (%)",
     type=float, minval=0.0, step=0.1, defval=3) * 0.01

// Calculate moving averages
fastSMA = sma(close, 20)
slowSMA = sma(close, 60)

// Calculate trading conditions
enterLong  = crossover(fastSMA, slowSMA)
enterShort = crossunder(fastSMA, slowSMA)

// Plot moving averages
plot(series=fastSMA, color=teal)
plot(series=slowSMA, color=orange)

// STEP 2:
// Determine trail stop loss prices
longStopPrice = 0.0, shortStopPrice = 0.0

longStopPrice := if (strategy.position_size > 0)
    stopValue = close * (1 - longTrailPerc)
    max(stopValue, longStopPrice[1])
else
    0

shortStopPrice := if (strategy.position_size < 0)
    stopValue = close * (1 + shortTrailPerc)
    min(stopValue, shortStopPrice[1])
else
    999999

// Plot stop loss values for confirmation
plot(series=(strategy.position_size > 0) ? longStopPrice : na,
     color=fuchsia, style=cross,
     linewidth=2, title="Long Trail Stop")
plot(series=(strategy.position_size < 0) ? shortStopPrice : na,
     color=fuchsia, style=cross,
     linewidth=2, title="Short Trail Stop")

// Submit entry orders
if (enterLong)
    strategy.entry(id="EL", long=true)

if (enterShort)
    strategy.entry(id="ES", long=false)

// STEP 3:
// Submit exit orders for trail stop loss price
if (strategy.position_size > 0)
    strategy.exit(id="XL TRL STP", stop=longStopPrice)

if (strategy.position_size < 0)
    strategy.exit(id="XS TRL STP", stop=shortStopPrice)
