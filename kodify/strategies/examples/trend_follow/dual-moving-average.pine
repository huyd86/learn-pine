// Source: https://kodify.net/tradingview/strategies/dual-moving-average/
// The Dual Moving Average trend-following strategy programmed for TradingView

//=== Trading rules of the Dual Moving Average strategy

Enter long and exit short rules:
Open a long position (and close open short positions) when the 100-day Simple Moving Average (SMA) crosses over the 350-day moving average.
Enter short and exit long rules:
Open a short position (and close any open long positions) when the 100-day moving average crosses under the 350-day moving average.
Position sizing:
For long and short positions, determine the position size by dividing 0.5% of equity by the value of the marketâ€™s 20-bar Average True Range (ATR) in terms of dollars.

//=== Full code: the Dual Moving Average strategy for TradingView

//@version=3

// 1. Define strategy settings
strategy(title="Dual Moving Average", overlay=true,
     pyramiding=0, initial_capital=100000,
     commission_type=strategy.commission.cash_per_order,
     commission_value=4, slippage=2,
     calc_on_every_tick=true)

fastMALen = input(title="Fast MA Length", type=integer, defval=100)
slowMALen = input(title="Slow MA Length", type=integer, defval=350)

usePosSize = input(title="Use Position Sizing?", type=bool, defval=true)
riskPerc   = input(title="Risk %", type=float, defval=0.5, step=0.25)

// 2. Calculate strategy values
fastMA = sma(close, fastMALen)
slowMA = sma(close, slowMALen)

riskEquity  = (riskPerc / 100) * strategy.equity
atrCurrency = (atr(20) * syminfo.pointvalue)
posSize     = usePosSize ? floor(riskEquity / atrCurrency) : 1

// 3. Output strategy data
plot(series=fastMA, color=teal, linewidth=2, title="Fast MA")
plot(series=slowMA, color=orange, linewidth=2, title="Slow MA")

bgColour = crossover(fastMA, slowMA) ? green :
     crossunder(fastMA, slowMA) ? red :
     na

bgcolor(color=bgColour, transp=90)

// 4. Determine long trading conditions
enterLong = crossover(fastMA, slowMA) and barstate.ishistory

// 5. Code short trading conditions
enterShort = crossunder(fastMA, slowMA) and barstate.ishistory

// 6. Submit entry orders
if (enterLong)
    strategy.entry(id="EL", long=true, qty=posSize)

if (enterShort)
    strategy.entry(id="ES", long=false, qty=posSize)

// 7. Submit exit orders
strategy.close_all(when=barstate.islast)
