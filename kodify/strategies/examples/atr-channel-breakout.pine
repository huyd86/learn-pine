// Source: https://kodify.net/tradingview/strategies/atr-channel-breakout/
// The ATR Channel breakout strategy coded for TradingView

//=== Trend following with the ATR Channel breakout strategy for TradingView

//=== Trading rules of the ATR Channel breakout strategy

The ATR Channel breakout strategy has the following trading rules (Faith, 2007):

Enter long rules:
Open a long position when the close is above the top of the channel, which is computed as follows: 350-day moving average + 7 x the 20-bar Average True Range (ATR).
Exit long rules:
Close the long position when prices cross below the 350-day moving average.
Enter short rules:
Open a short position when the close is below the bottom of the channel, computed as follows: 350-day moving average - 3 x ATR.
Exit short rules:
Cover the short position when prices cross above the 350-day moving average.
Position sizing:
For long and short positions, determine the position size by dividing 0.5% of equity by the value of the marketâ€™s 20-bar Average True Range (ATR) in terms of dollars.

//=== Complete code of the ATR Channel breakout strategy for TradingView

//@version=4

// 1. Define strategy settings
strategy(title="ATR Channel Breakout", overlay=true,
     pyramiding=0, initial_capital=100000,
     commission_type=strategy.commission.cash_per_order,
     commission_value=4, slippage=2)

smaLength = input(title="SMA Length", type=input.integer, defval=350)
atrLength = input(title="ATR Length", type=input.integer, defval=20)

ubOffset = input(title="Upperband Offset", type=input.float, defval=7, step=0.50)
lbOffset = input(title="Lowerband Offset", type=input.float, defval=3, step=0.50)

usePosSize = input(title="Use Position Sizing?", type=input.bool, defval=true)
riskPerc   = input(title="Risk %", type=input.float, defval=0.5, step=0.25)


// 2. Calculate strategy values
smaValue = sma(close, smaLength)
atrValue = atr(atrLength)

upperBand = smaValue + (ubOffset * atrValue)
lowerBand = smaValue - (lbOffset * atrValue)

riskEquity  = (riskPerc / 100) * strategy.equity
atrCurrency = (atrValue * syminfo.pointvalue)
posSize     = usePosSize ? floor(riskEquity / atrCurrency) : 1


// 3. Output strategy data
plot(series=smaValue, title="SMA", color=color.orange)

plot(series=upperBand, title="UB", color=color.green,
     linewidth=2)
plot(series=lowerBand, title="LB", color=color.red,
     linewidth=2)


// 4. Determine long trading conditions
enterLong = crossover(close, upperBand)
exitLong  = crossunder(close, smaValue)


// 5. Code short trading conditions
enterShort = crossunder(close, lowerBand)
exitShort  = crossover(close, smaValue)


// 6. Submit entry orders
if (enterLong)
    strategy.entry(id="EL", long=true, qty=posSize)

if (enterShort)
    strategy.entry(id="ES", long=false, qty=posSize)


// 7. Submit exit orders
strategy.close(id="EL", when=exitLong)
strategy.close(id="ES", when=exitShort)
