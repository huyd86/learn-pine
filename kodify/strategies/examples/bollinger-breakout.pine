// Source: https://kodify.net/tradingview/strategies/bollinger-breakout/
// Programming the Bollinger Breakout strategy for TradingView

//=== Trend following with the Bollinger Breakout strategy for TradingView

//=== Trading rules for the Bollinger Breakout strategy

Enter long rules:
Open a long position when the close is above the upper Bollinger band, which is based on a 350-day moving average incremented with 2.5 standard deviations.
Exit long rules:
Sell the long position when prices cross below the 350-day moving average.
Enter short rules:
Open a short position when the close is below the lower Bollinger band, based on a 350-day moving average decreased with 2.5 standard deviations.
Exit short rules:
Cover the short position when price cross above the 350-day moving average.
Position sizing:
For long and short positions, determine the position by dividing 0.5% of equity by the value of the marketâ€™s 20-bar Average True Range (ATR) in terms of dollars.

//=== Full code: the Bollinger Breakout strategy for TradingView

//@version=3
// 1. Define strategy settings
strategy(title="Bollinger Breakout", overlay=true,
     pyramiding=0, initial_capital=100000,
     commission_type=strategy.commission.cash_per_order,
     commission_value=4, slippage=2)

smaLength = input(title="SMA Length", type=integer, defval=350)
stdLength = input(title="StdDev Length", type=integer, defval=350)

ubOffset = input(title="Upper Band Offset", type=float, defval=2.5, step=0.5)
lbOffset = input(title="Lower Band Offset", type=float, defval=2.5, step=0.5)

usePosSize = input(title="Use Position Sizing?", type=bool, defval=true)
riskPerc   = input(title="Risk %", type=float, defval=0.5, step=0.25)

// 2. Calculate strategy values
smaValue = sma(close, smaLength)
stdDev   = stdev(close, stdLength)

upperBand = smaValue + (stdDev * ubOffset)
lowerBand = smaValue - (stdDev * lbOffset)

riskEquity  = (riskPerc / 100) * strategy.equity
atrCurrency = (atr(20) * syminfo.pointvalue)
posSize     = usePosSize ? floor(riskEquity / atrCurrency) : 1

// 3. Output strategy data
plot(series=smaValue, title="SMA", color=teal)

plot(series=upperBand, title="UB", color=green,
     linewidth=2)
plot(series=lowerBand, title="LB", color=red,
     linewidth=2)

// 4. Determine long trading conditions
enterLong = crossover(close, upperBand)
exitLong  = crossunder(close, smaValue)

// 5. Code short trading conditions
enterShort = crossunder(close, lowerBand)
exitShort  = crossover(close, smaValue)

// 6. Submit entry orders
if (enterLong)
    strategy.entry(id="EL", long=true, qty=posSize)

if (enterShort)
    strategy.entry(id="ES", long=false, qty=posSize)

// 7. Submit exit orders
strategy.close(id="EL", when=exitLong)
strategy.close(id="ES", when=exitShort)
