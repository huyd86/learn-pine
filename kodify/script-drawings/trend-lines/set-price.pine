// Source: https://kodify.net/tradingview/lines/set-price/
// How to change the price coordinates of a TradingView trend line?

//@version=4
study(title="Update line price coordinates - example", overlay=true)

// Make both trend lines just once
var highLine = line.new(x1=bar_index[1], y1=high[1],
     x2=bar_index, y2=high, color=color.green, width=3)

var lowLine = line.new(x1=bar_index[1], y1=low[1],
     x2=bar_index, y2=low, color=color.red, width=3)

// Find price consolidations
consolidate = (volume < volume[1]) and
     (volume[1] < volume[2]) and
     (tr < tr[1]) and (tr[1] < tr[2])

var insideRange = false

// When there's a price consolidation, update the
// line to capture the 10-bar price range
if (consolidate)
    hiHighs = highest(high, 10)[1]
    loLows  = lowest(low, 10)[1]

    // Change coordinates high line
    line.set_y2(id=highLine, y=hiHighs)
    line.set_y1(id=highLine, y=hiHighs)

    line.set_x1(id=highLine, x=bar_index[10])
    line.set_x2(id=highLine, x=bar_index)

    // Update coordinates of low line
    line.set_y1(id=lowLine, y=loLows)
    line.set_y2(id=lowLine, y=loLows)

    line.set_x1(id=lowLine, x=bar_index[10])
    line.set_x2(id=lowLine, x=bar_index)

    insideRange := true

// Fetch the line prices (which works since
// the lines are horizontal)
highPrice = line.get_y2(id=highLine)
lowPrice  = line.get_y2(id=lowLine)

// When prices remain in the consolidation range,
// extend the lines
insideRange := insideRange and (close > lowPrice)
     and (close < highPrice)

if (insideRange[1])
    line.set_x2(id=highLine, x=bar_index)
    line.set_x2(id=lowLine, x=bar_index)
    
